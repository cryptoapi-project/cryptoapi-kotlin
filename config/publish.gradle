apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

ext {
    bintrayRepo = 'mobile'
    bintrayName = 'cryptoapi-stable'

    publishedGroupId = 'io.pixelplex.mobile.cryptoapi'
    libraryName = 'cryptoapi'
    artifact = POM_ARTIFACT_ID
    packagingType = POM_PACKAGING
    libraryDescription = 'Framework for working with CryptoAPI'

    gitUrl = 'https://github.com/cryptoapi-project/cryptoapi-kotlin'
    siteUrl = 'https://apikey.io'

    licenseName = 'MIT LICENSE'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

Properties properties = new Properties()

if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

// Bintray account name for library publishing
def bintrayAccount = properties.getProperty("bintrayUser")
if (bintrayAccount == null && project.hasProperty("bintrayUser")) {
    bintrayAccount = project.findProperty("bintrayUser")
}

// Bintray api key for library publishing
def bintrayKey = properties.getProperty("bintrayApiKey")
if (bintrayKey == null && project.hasProperty("bintrayApiKey")) {
    bintrayKey = project.findProperty("bintrayApiKey")
}

// Library version
def libraryVersion = properties.getProperty("libraryVersion")
if (libraryVersion == null && project.hasProperty("libraryVersion")) {
    libraryVersion = project.findProperty("libraryVersion")
}

version = libraryVersion
group = publishedGroupId

install {
    repositories.mavenInstaller {
        pom.project {
            packaging packagingType
            groupId publishedGroupId
            artifactId artifact

            name libraryName
            description libraryDescription
            url siteUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id 'pixelplex'
                    name 'pixelplex inc'
                    email 'info@pixelplex.io'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
            }
        }
        pom.withXml {
            def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')
            // Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
            configurations.implementation.allDependencies.each {
                // Ensure dependencies such as fileTree are not included.
                if (it.name != 'unspecified') {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from "$projectDir/src/main/java"
}

task javadocJar(type: Jar, dependsOn: dokka) {
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = "Assembles framework docs with Dokka"
    classifier = "javadoc"
    from(dokka)
}

artifacts {
    archives javadocJar
    archives sourcesJar
}


task validateBintrayCredentials() {
    doFirst {
        if (bintrayAccount == null || bintrayKey == null) {
            throw new GradleException("Account name and api key required for bintray publishing")
        }
    }
}

bintrayUpload.dependsOn(validateBintrayCredentials)

bintray {
    user = bintrayAccount
    key = bintrayKey

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        userOrg = 'pixelplex'
        vcsUrl = gitUrl
        licenses = allLicenses
        dryRun = false
        publish = true
        override = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}
